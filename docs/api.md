# API

This document provides all of the API documentation for `reason-urql`.

## Hooks

`reason-urql` comes with a set of custom hooks to use in your ReasonReact components, including `useQuery`, `useMutation`, and `useSubscription`. These are fully type safe and will correctly infer the type of your GraphQL response if using `graphql_ppx`.

### `useQuery`

`useQuery` allows you to execute a GraphQL query.

#### Arguments

| Argument        | Type                          | Description                                                                                                                                                                                                                                                              |
| --------------- | ----------------------------- | ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------ |
| `request`       | `Types.request`               | Your `graphql_ppx` request representing your query. Generated by calling `.make()` on the `graphql_ppx` module. If you're not using `graphql_ppx`, pass a `Js.t` of the following shape: `{. "query": string, "variables": Js.Json.t, "parse": Js.Json.t => 'response }` |
| `requestPolicy` | `option(Types.requestPolicy)` | The request policy to use to execute the query. Defaults to `"cache-first"`.                                                                                                                                                                                             |
| `pause`         | `option(bool)`                | A boolean flag instructing `useQuery` to pause execution of the subsequent query operation.                                                                                                                                                                              |

#### Return Type – `(hookResponse('response), option(partialOperationContext) => unit)`

`useQuery` returns a tuple containing the result of executing your GraphQL query as a record, `hookResponse`, and a function for re-executing the query imperatively, `executeQuery`.

| Return Value   | Type                                      | Description                                                                                                                                                                                                                       |
| -------------- | ----------------------------------------- | --------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------- |
| `state`        | `hookResponse('response)`                 | A record containing fields for `fetching`, `data`, `error`, and `response`. `response` is a variant containing constructors for `Data`, `Error`, `Fetching` and `NotFound`. Useful for pattern matching to render conditional UI. |
| `executeQuery` | `option(partialOperationContext) => unit` | A function for imperatively re-executing the query. Accepts a `partialOperationContext` for altering the request.                                                                                                                 |

#### Example

```reason
module GetPokemon = [%graphql
  {|
  query pokemon($name: String!) {
    pokemon(name: $name) {
      name
      classification
      image
    }
  }
|}
];

[@react.component]
let make = () => {
  let request = GetPokemon.make(~name="Cubone", ());
  let ({response}, _) = useQuery(~request, ());

  switch (response) {
    | Fetching => /* Render loading UI. */
    | Data(d) => /* Render UI with data. d represents the data returned by your GraphQL query.  */
    | Error(e) => /* Render error handling UI. e represents the CombinedError
    instance returned (if any) when executing your GraphQL query. */
    | NotFound => /* Render fallback UI. This may occur if your GraphQL server is not running. */
  }
}
```

### `useMutation`

`useMutation` allows you to execute a GraphQL mutation.

#### Arguments

| Argument  | Type            | Description                                                                                                                                                                                                                                                              |
| --------- | --------------- | ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------ |
| `request` | `Types.request` | Your `graphql_ppx` request representing your query. Generated by calling `.make()` on the `graphql_ppx` module. If you're not using `graphql_ppx`, pass a `Js.t` of the following shape: `{. "query": string, "variables": Js.Json.t, "parse": Js.Json.t => 'response }` |

#### Return Type – `(hookResponse('response), unit => Js.Promise.t(UrqlTypes.operationResult))`

`useMutation` returns a tuple containing the result of executing your GraphQL mutation as a record, `hookResponse`, and a function for executing the mutation imperatively, `executeMutation`. By default, `useMutation` **does not** execute your mutation when your component renders – rather, it is up to you to call `executeMutation` when you want to (i.e. by attaching it to on an event handler or running it inside of an effect).

| Return Value      | Type                                              | Description                                                                                                                                                                                                                       |
| ----------------- | ------------------------------------------------- | --------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------- |
| `state`           | `hookResponse('response)`                         | A record containing fields for `fetching`, `data`, `error`, and `response`. `response` is a variant containing constructors for `Data`, `Error`, `Fetching` and `NotFound`. Useful for pattern matching to render conditional UI. |
| `executeMutation` | `unit => Js.Promise.t(UrqlTypes.operationResult)` | A function for imperatively executing the mutation.                                                                                                                                                                               |

#### Example

```reason
module LikeDog = [%graphql
    {|
    mutation likeDog($key: ID!) {
      likeDog(key: $key) {
        likes
      }
    }
  |}
  ];

[@react.component]
let make = (~id) => {
  let request = Mutations.LikeDog.make(~key=id, ());
  let (_, executeMutation) = Hooks.useMutation(~request);

  <button onClick={_e => executeMutation() |> ignore}>
    "Like Me"->React.string
  </button>
}
```

### `useSubscription`

`useSubscription` allows you to execute a GraphQL subscription.

#### Arguments

| Argument  | Type            | Description                                                                                                                                                                                                                                                              |
| --------- | --------------- | ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------ |
| `request` | `Types.request` | Your `graphql_ppx` request representing your query. Generated by calling `.make()` on the `graphql_ppx` module. If you're not using `graphql_ppx`, pass a `Js.t` of the following shape: `{. "query": string, "variables": Js.Json.t, "parse": Js.Json.t => 'response }` |
| `handler` | `handler`       | A variant type to allow for proper type inference of a `handler` function. A `handler` function allows you to accumulate subscription responses in the `data` field of the returned state record.                                                                        |

#### Return Type – `hookResponse('response)`

`useSubscription` returns the result of executing your GraphQL subscription as a record of type `hookResponse`.

| Return Value | Type                      | Description                                                                                                                                                                                                                       |
| ------------ | ------------------------- | --------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------- |
| `state`      | `hookResponse('response)` | A record containing fields for `fetching`, `data`, `error`, and `response`. `response` is a variant containing constructors for `Data`, `Error`, `Fetching` and `NotFound`. Useful for pattern matching to render conditional UI. |

#### Example

```reason
module SubscribeRandomInt = [%graphql
  {|
  subscription subscribeNumbers {
    newNumber @bsDecoder(fn: "string_of_int")
  }
|}
];

let handler = (prevSubscriptions, subscription) => {
  switch (prevSubscriptions) {
  | Some(subs) => Array.append(subs, [|subscription|])
  | None => [|subscription|]
  };
};

[@react.component]
let make = () => {
  let {response} =
    useSubscription(~request=SubscribeRandomInt.make(), ~handler=Handler(handler));

  switch (response) {
    | Fetching => /* Render loading UI. */
    | Data(d) => /* Render UI with data. d here represents the array of subscriptions accumulated by handler  */
    | Error(e) => /* Render error handling UI. e represents the CombinedError
    instance returned (if any) when executing your GraphQL query. */
    | NotFound => /* Render fallback UI. This may occur if your GraphQL server is not running. */
  }
}
```

## Components

### `Query`

The `Query` component is the core building block of `reason-urql`. Use this component to query your GraphQL API and render UI with the result.

#### Props

| Prop            | Type                          | Description                                                                  |
| --------------- | ----------------------------- | ---------------------------------------------------------------------------- |
| `query`         | `string`                      | The GraphQL query to execute.                                                |
| `variables`     | `option(Js.Json.t)`           | Variables to be passed to the GraphQL query.                                 |
| `requestPolicy` | `option(Types.requestPolicy)` | The request policy to use to execute the query. Defaults to `"cache-first"`. |

#### Render Props

| Prop           | Type                                                       | Description                                                                                                                                    |
| -------------- | ---------------------------------------------------------- | ---------------------------------------------------------------------------------------------------------------------------------------------- |
| `fetching`     | `bool`                                                     | A boolean flag to indicate if the request is currently executing.                                                                              |
| `data`         | `'a`                                                       | The data returned by your GraphQL API.                                                                                                         |
| `error`        | `CombinedError.t`                                          | The error(s), if any, returned by the GraphQL operation.                                                                                       |
| `executeQuery` | `option(Js.Json.t) => Js.Promise.t(Types.operationResult)` | A callback to manually execute the query operation. Useful to imperatively execute the query. Accepts variables required to execute the query. |
| `response`     | `Types.response('a)`                                       | A variant containing constructors for `Data`, `Error`, `Fetching` and `NotFound`. Useful for pattern matching to render conditional UI.        |

#### Example

```reason
open ReasonUrql;

module GetAllDogs = [%graphql
  {|
  query dogs {
    dogs {
      name
      breed
      likes
    }
  }
|}
];

let query = GetAllDogs.make()##query;

let component = ReasonReact.statelessComponent("Example");

let make = (_children) => {
  ...component,
  render: _self => {
    <Query query>
      {...({ response }): Query.queryRenderProps(GetAllDogs.t) => {
        switch (response) {
          | Fetching => /* Render loading UI. */
          | Data(d) => /* Render UI with data. */
          | Error(e) => /* Render error handling UI. */
          | NotFound => /* Render fallback UI. This may occur if your GraphQL server is not running. */
        }
      }}
    </Query>
  }
}
```

**Check out the `2-query` example in the `examples` directory to learn more about using the `Query` component.**

### `Mutation`

The `Mutation` component allows you to imperatively execute mutations. Use this component to pass the `executeMutation` function to your UI.

#### Props

| Prop    | Type     | Description                      |
| ------- | -------- | -------------------------------- |
| `query` | `string` | The GraphQL mutation to execute. |

#### Render Props

| Prop              | Type                                                       | Description                                                                                                                                             |
| ----------------- | ---------------------------------------------------------- | ------------------------------------------------------------------------------------------------------------------------------------------------------- |
| `fetching`        | `bool`                                                     | A boolean flag to indicate if the request is currently executing.                                                                                       |
| `data`            | `'a`                                                       | The data returned by your GraphQL API.                                                                                                                  |
| `error`           | `CombinedError.t`                                          | The error(s), if any, returned by the GraphQL operation.                                                                                                |
| `executeMutation` | `option(Js.Json.t) => Js.Promise.t(Types.operationResult)` | A callback to manually execute the mutation operation. Useful to imperatively execute the mutation. Accepts variables required to execute the mutation. |
| `response`        | `Types.response('a)`                                       | A variant containing constructors for `Data`, `Error`, `Fetching` and `NotFound`. Useful for pattern matching to render conditional UI.                 |

#### Example

```reason
open ReasonUrql;

module LikeDog = [%graphql
  {|
  mutation likeDog($key: ID!) {
    likeDog(key: $key) {
      likes
    }
  }
|}
];

let request = GetAllDogs.make(~key="VmeRTX7j-", ());
let mutation = request##query;
let variables = request##variables;

let component = ReasonReact.statelessComponent("Example");

let make = (_children) => {
  ...component,
  render: _self => {
    <Mutation query=mutation>
      {...({ executeMutation }): Mutation.mutationRenderProps(LikeDog.t) => {
        <button onClick={(_e) => executeMutation(Some(variables))}>
          "Click Me to Execute the Mutation" -> ReasonReact.string
        </button>
      }}
    </Mutation>
  }
}
```

**Check out the `3-mutation` example in the `examples` directory to learn more about using the `Mutation` component.**

### `Subscription`

The `Subscription` component allows you to subscribe to GraphQL subscriptions emitted by your API. Use this component to render UI with realtime data.

#### Props

| Prop        | Type                                                        | Description                                                                                                                                |
| ----------- | ----------------------------------------------------------- | ------------------------------------------------------------------------------------------------------------------------------------------ |
| `query`     | `string`                                                    | The GraphQL subscription to execute.                                                                                                       |
| `variables` | `option(Js.Json.t)`                                         | Variables to be passed to the GraphQL subscription.                                                                                        |
| `handler`   | `(~prevSubscriptions: option('a), ~subscription: 'b) => 'a` | A function to control how to aggregate subscription results, given the accumulation of `prevSubscriptions` and the current `subscription`. |

#### Render Props

| Prop       | Type                 | Description                                                                                                                             |
| ---------- | -------------------- | --------------------------------------------------------------------------------------------------------------------------------------- |
| `fetching` | `bool`               | A boolean flag to indicate if the request is currently executing.                                                                       |
| `data`     | `'a`                 | The data returned by your GraphQL API.                                                                                                  |
| `error`    | `CombinedError.t`    | The error(s), if any, returned by the GraphQL operation.                                                                                |
| `response` | `Types.response('a)` | A variant containing constructors for `Data`, `Error`, `Fetching` and `NotFound`. Useful for pattern matching to render conditional UI. |

#### Example

```reason
open ReasonUrql;

/* You'll need to create your own subscription client here. Check out examples/5-subscription
to see an example using subscription-transport-ws, which is the protocol used by apollo-server. */
let forwardSubscription = operation => /* Send operation to your subscription client. */
let subscriptionExchangeOpts = Exchanges.subscriptionExchangeOpts(~forwardSubscription);

let client = Client.make(
  ~url="https://localhost:3000/graphql",
  ~exchanges=[|Exchanges.subscriptionExchange(subscriptionExchangeOpts)|],
  ()
);

module SubscribeMessages = [%graphql
  {|
  subscription subscribeMessages {
    newMessage {
      id
      message
    }
  }
|}
];

let query = SubscribeMessages.make()##query;

let component = ReasonReact.statelessComponent("Messages");

let make = _children => {
  ...component,
  render: _self =>
    <Subscription query>
      ...{({response}) =>
        switch (response) {
          | Fetching => /* Render loading UI. */
          | Data(d) => /* Render UI with subscription data. */
          | Error(e) => /* Render error handling UI. */
          | NotFound => /* Render fallback UI. This may occur if your GraphQL server is not running. */
        }
      }
    </Subscription>,
};
```

**Check out the `35-subscription` example in the `examples` directory to learn more about using the `Subscription` component.**

### `Provider`

The `Provider`'s responsibility is to pass the `urql` client instance down to `Query`, `Mutation`, and `Subscription` components through context. Wrap the root of your application with `Provider`.

#### Props

| Prop     | Type       | Description                 |
| -------- | ---------- | --------------------------- |
| `client` | `Client.t` | The `urql` client instance. |

#### Example

```reason
open ReasonUrql;

let client = Client.make(~url="https://localhost:3000/graphql", ());

ReactDOMRe.renderToElementWithId(<Provider client><App /></Provider>, "root");
```

## `Client`

The client is the central orchestrator of `reason-urql`, and is responsible for executing queries, mutations, and subscriptions passed to the `Query`, `Mutation`, and `Subscription` components.

To create a client, simple call the `make` function from the `Client` module.

```reason
open ReasonUrql;

let client = Client.make(~url="https://localhost:3000/graphql", ());
```

The client optionally accepts a `fetchOptions` argument for passing additional properties to `fetch` requests (i.e. authorization headers) and an `exchanges` argument for customizing the exchanges that operations are passed through.

### `Client.make`

Instantiate an `urql` client instance.

```reason
(
  ~url: string,
  ~fetchOptions: option(fetchOptions)=?,
  ~exchanges: option(array(Exchanges.exchange))=?,
  unit
) => Client.t
```

#### Arguments

| Argument       | Type                                  | Description                                       |
| -------------- | ------------------------------------- | ------------------------------------------------- |
| `url`          | `string`                              | The url of your GraphQL API.                      |
| `fetchOptions` | `option(fetchOptions)=?`              | Optional fetch options to be used by your client. |
| `exchanges`    | `option(array(Exchanges.exchange))=?` | The array of exchanges to be used by your client. |

#### Examples

**Create a client with just a `url`.**

```reason
open ReasonUrql;

let client = Client.make(~url="https://localhost:3000/graphql", ());
```

**Create a client with `fetchOptions`.**

```reason
open ReasonUrql;

let fetchOptions =
  Client.FetchOpts(Fetch.RequestInit.make(
    ~method_=Post,
    ~headers=Fetch.HeadersInit.make({"Content-Type": "application/json"}),
    (),
  ));

let client = Client.make(~url="https://localhost:3000/graphql", ~fetchOptions, ());
```

**Create a client with `exchanges`.**

```reason
open ReasonUrql;

let client = Client.make(
  ~url="https://localhost:3000/graphql",
  ~exchanges=[|
    Exchanges.fetchExchange,
    Exchanges.dedupExchange,
    Exchanges.cacheExchange,
    Exchanges.debugExchange
  |],
  ()
);
```

### `Client.executeQuery`

Imperatively execute a GraphQL query operation.

```reason
(
  ~client: Client.t,
  ~query: Types.graphqlRequest,
  ~opts: option(Types.partialOperationContext)=?,
  unit
) =>
Wonka.Types.sourceT('a) =
"";
```

| Argument | Type                                      | Description                                                                                                  |
| -------- | ----------------------------------------- | ------------------------------------------------------------------------------------------------------------ |
| `client` | `Client.t`                                | The `urql` client instance.                                                                                  |
| `query`  | `Types.graphqlRequest`                    | The GraphQL query request object, consisting of a `query` string and, optionally, a `variables` `Js.Json.t`. |
| `opts`   | `option(Types.partialOperationContext)=?` | Additional options to pass to the operation.                                                                 |

#### Example

```reason
open ReasonUrql;

let client = Client.make(~url="https://localhost:3000/graphql", ());

module GetAllDogs = [%graphql
  {|
  query dogs {
    dogs {
      name
      breed
      likes
    }
  }
|}
];

let query = Request.createRequest(~query=GetAllDogs.make()##query, ());

Client.executeQuery(~client, ~query, ())
  |> Wonka.subscribe((. result) => {
    Js.log2("Result: ", result);
  });
```

#### `Client.executeMutation`

Execute a GraphQL mutation operation.

```reason
(
  ~client: t,
  ~mutation: UrqlTypes.graphqlRequest,
  ~opts: UrqlTypes.partialOperationContext=?,
  unit
) =>
Wonka.Types.sourceT('a) =
"";
```

| Argument   | Type                                      | Description                                                                                                     |
| ---------- | ----------------------------------------- | --------------------------------------------------------------------------------------------------------------- |
| `client`   | `Client.t`                                | The `urql` client instance.                                                                                     |
| `mutation` | `Types.graphqlRequest`                    | The GraphQL mutation request object, consisting of a `query` string and, optionally, a `variables` `Js.Json.t`. |
| `opts`     | `option(Types.partialOperationContext)=?` | Additional options to pass to the operation.                                                                    |

#### Example

```reason
open ReasonUrql;

let client = Client.make(~url="https://localhost:3000/graphql", ());

module LikeDog = [%graphql
  {|
  mutation likeDog($key: ID!) {
    likeDog(key: $key) {
      name
      breed
      likes
    }
  }
|}
];

let mutation = LikeDog.make(~key="VmeRTX7j-", ());

let mutationRequest =
  Request.createRequest(
    ~query=mutation##query,
    ~variables=mutation##variables,
    (),
  );

Client.executeQuery(~client, ~mutation=mutationRequest, ())
  |> Wonka.subscribe((. result) => {
    Js.log2("Result: ", result);
  });
```

#### `Client.executeSubscription`

Execute a GraphQL subscription operation.

```reason
(
  ~client: t,
  ~subscription: UrqlTypes.graphqlRequest,
  ~opts: option(UrqlTypes.partialOperationContext)=?,
  unit
) =>
Wonka.Types.sourceT('a) =
"";
```

| Argument       | Type                                      | Description                                                                                                         |
| -------------- | ----------------------------------------- | ------------------------------------------------------------------------------------------------------------------- |
| `client`       | `Client.t`                                | The `urql` client instance.                                                                                         |
| `subscription` | `Types.graphqlRequest`                    | The GraphQL subscription request object, consisting of a `query` string and, optionally, a `variables` `Js.Json.t`. |
| `opts`         | `option(Types.partialOperationContext)=?` | Additional options to pass to the operation.                                                                        |

#### Example

```reason
open ReasonUrql;'

/* You'll need to create your own subscription client here. Check out examples/5-subscription
to see an example using subscription-transport-ws, which is the protocol used by apollo-server. */
let forwardSubscription = operation => /* Send operation to your subscription client. */
let subscriptionExchangeOpts = Exchanges.subscriptionExchangeOpts(~forwardSubscription);

let client = Client.make(
  ~url="https://localhost:3000/graphql",
  ~exchanges=[|Exchanges.subscriptionExchange(subscriptionExchangeOpts)|],
  ()
);

module SubscribeMessages = [%graphql
  {|
  subscription subscribeMessages {
    newMessage {
      id
      message
    }
  }
|}
];

let query = SubscribeMessages.make()##query;
let subscription = Request.createRequest(~query, ());

Client.executeSubscription(~client, ~subscription, ())
  |> Wonka.subscribe((. result) => {
    Js.log2("Result: ", result);
  });
```
